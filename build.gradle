plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "io.gitlab.arturbosch.detekt" version "1.10.0"
    id 'io.franzbecker.gradle-lombok' version '3.3.0'
}

allprojects {

    group "org.sheinbergon"
    version "0.1.0"

    sourceCompatibility = 11

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

subprojects {

    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'java-library'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    apply plugin: 'io.franzbecker.gradle-lombok'

    dependencies {
        api "net.java.dev.jna:jna-platform:5.5.0"
        api "com.google.guava:guava:29.0-jre"

        testImplementation "com.natpryce:hamkrest:1.7.0.3"
        testImplementation "org.amshove.kluent:kluent:1.61"
        testImplementation "org.jetbrains.kotlinx:atomicfu:0.14.3"
        testImplementation "org.jetbrains.kotlin:kotlin-stdlib"

        testImplementation 'org.junit.platform:junit-platform-runner:1.6.2'
        testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    }

    compileTestKotlin {
        dependsOn('detekt')
        kotlinOptions {
            jvmTarget = 11
        }
    }

    compileKotlin {
        dependsOn('detekt')
        kotlinOptions {
            jvmTarget = 11
        }
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
            events "passed", "skipped", "failed"
        }
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            csv.enabled false
            html.enabled true
        }
    }

    checkstyle {
        toolVersion '8.34'
        configFile file("$rootDir/checkstyle.xml")
        configDir = rootDir
    }

    detekt {
        toolVersion = "1.10.0"
        config = files("$rootDir/detekt.yml")
        input = files("src/main/kotlin", "src/test/kotlin")
        buildUponDefaultConfig = true
    }
}

wrapper {
    gradleVersion = "6.3"
}